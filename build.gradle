plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'pmd'
    id 'checkstyle'

    id 'com.github.spotbugs' version '4.6.0'
    id 'io.freefair.lombok' version '5.3.3.3'
    id 'com.github.ben-manes.versions' version '0.42.0'

    // https://docs.spring.io/spring-boot/docs/current/reference/html/dependency-versions.html
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'

    id "com.vaadin" version "23.1.3"
}
apply from: './gradle/dependencies.gradle'
apply from: './gradle/checking.gradle'
apply from: './gradle/testing.gradle'

generateLombokConfig.enabled = false

group = theGroup
archivesBaseName = theArchivesBaseName
idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
compileJava {
    options.encoding = 'UTF-8'
}
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}
wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.9.2'
    doLast {
        delete "$projectDir/gradlew.bat", "$projectDir/gradlew"
    }
}
vaadin {
    nodeVersion = '16.15.0'
    pnpmEnable = true
    productionMode = true
}
classes {
    doLast {
        def resourcesDir = sourceSets.main.output.resourcesDir as File
        def releaseVersion = System.env.RELEASE_VERSION as String
        if (releaseVersion) {
            new File(resourcesDir, "version.info").text = releaseVersion
        }
    }
}